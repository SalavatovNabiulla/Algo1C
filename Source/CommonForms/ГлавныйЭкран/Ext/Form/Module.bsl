
&НаКлиенте
Процедура ПроверитьРешение(Команда)
	Если КонтекстИсполнения Тогда
		РезультатИсполнения = РаботаСКодомКлиент.ИсполнитьКод(Код);
	Иначе
		РезультатИсполнения = РаботаСКодомСервер.ИсполнитьКод(Код);
	КонецЕсли;
	//
	Если РезультатИсполнения.Свойство("Ошибка") Тогда
		ПредупреждениеАсинх(РезультатИсполнения.Ошибка,0,"Ошибка исполнения");
	Иначе
		Вывод = РезультатИсполнения.Вывод;
		ВремяИсполненияСтрока = Строка((РезультатИсполнения.ВремяКонец - РезультатИсполнения.ВремяНачало)/1000) + " сек.";
		ПредупреждениеАсинх(ВремяИсполненияСтрока,0,"Время исполнения");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЗадачу(Команда)
	Если Не ЗначениеЗаполнено(НаименованиеЗадачи) Тогда
		ПредупреждениеАсинх("Наименование задачи не заполнено!",0,"Запись задачи");
		Возврат;
	Иначе
		СохранитьЗадачуНаСервере();
		ПредупреждениеАсинх("Задача сохранена!",0,"Запись задачи");
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗадачуНаСервере()
	Если ЗначениеЗаполнено(ЗадачаОбъект) Тогда
		НовыйЗадачаОбъект = ЗадачаОбъект.ПолучитьОбъект();
	Иначе
		НовыйЗадачаОбъект = Справочники.Задачи.СоздатьЭлемент();
	КонецЕсли;
	//
	НовыйЗадачаОбъект.Наименование = НаименованиеЗадачи;
	НовыйЗадачаОбъект.КодРешения = Код;
	НовыйЗадачаОбъект.СсылкаНаЗадачу = СсылкаНаЗадачу;
	НовыйЗадачаОбъект.Записать();
	//
	ЗадачаОбъект = НовыйЗадачаОбъект.Ссылка;
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьЗадачуНаСервере()
	НаименованиеЗадачи = ЗадачаОбъект.Наименование;
	Код = ЗадачаОбъект.КодРешения;
	СсылкаНаЗадачу = ЗадачаОбъект.СсылкаНаЗадачу;
	Вывод = "";
КонецПроцедуры


&НаКлиенте
Асинх Процедура ЗагрузитьЗадачу(Команда)
	Обещание = ВвестиЗначениеАсинх(ЗадачаОбъект,"Выберите задачу",Тип("СправочникСсылка.Задачи"));
	ЗадачаОбъект = Ждать Обещание;
	Если ЗначениеЗаполнено(ЗадачаОбъект) Тогда
		ЗагрузитьЗадачуНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НоваяЗадачаСервер()
	ЗадачаОбъект = Справочники.Задачи.ПустаяСсылка();
	НаименованиеЗадачи = "";
	Код = "";
	СсылкаНаЗадачу = "";
	Вывод = "";
КонецПроцедуры

&НаКлиенте
Процедура НоваяЗадача(Команда)
	НоваяЗадачаСервер();
КонецПроцедуры

